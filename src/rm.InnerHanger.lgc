[*********************************************************************
[
[ rm.InnerHanger
[
[ 
[
[*********************************************************************

[***************************************
[ LOCAL DEFINES
[***************************************

[ add local defines here

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if(newRoom) {
    [ load, draw and discard the picture for the current room
    load.pic(currentRoom);
    draw.pic(currentRoom);
    discard.pic(currentRoom);
    
    [ we're roger now. Drop Grahams inventory
    drop("Summons");
    drop("Hat");
    drop("Teapot");
    drop("Teacup");
    drop("Tea");

    unanimate.all();
    assignn(vRoomInited, valTrue);

    load.view(vw.Roger);
    load.view(vw.RogerSpacesuitBroom);
    load.view(vw.RogerSpacesuitA);
    load.view(vw.Splat);
    load.view(vw.BigShuttle);

    animate.obj(ego);
    animate.obj(objHangerSplat1);
    animate.obj(objHangerSplat2);
    animate.obj(objBigShuttle);

    set.view(objHangerSplat1, vw.Splat);
    set.view(objHangerSplat2, vw.Splat);
    set.view(objBigShuttle, vw.BigShuttle);

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)) {
            assignn(vEgoX, 20);
            assignn(vEgoY, 100);
            assignn(vSweeping, valTrue);
            set(fWearingSuit);
            get("Broom");

            assignn(vSplat1Cleaned, valFalse);
            assignn(vSplat2Cleaned, valFalse);
            assignn(vHasOrders, valFalse);
            assignn(vCleanedUp, valFalse);

            position(objHangerSplat1, 100, 150);
            position(objHangerSplat2, 40, 90);  
        
            print("What is going on here?! Your dreams get more and more wild. You find yourself in a castle fortress unlike anything you have ever seen. Your suit of armor seems to be made of some kind of magical cloth. The stars are so close and bright that you could almost reach out and touch them. HOW STRANGE?!? And.. you're a janitor... Seriously?! Wow! That Daventry clover tea really packs a punch. The longer your dream continues the more familiar the situation feels.");
        }
    }

    if(isset(fWearingSuit)) {
        if(equaln(vSweeping, valTrue)) {
            set.view(ego, vw.RogerSpacesuitBroom);
        }
        else {
            set.view(ego, vw.RogerSpacesuitA);
        }    
    }
    else {
        set.view(ego, vw.Roger);
    }

    start.cycling(ego);

    stop.cycling(objHangerSplat1);
    stop.cycling(objHangerSplat2);
    stop.cycling(objBigShuttle);

    position(objBigShuttle, 50, 120);
    position.v(ego, vEgoX, vEgoY);

    set.priority(objBigShuttle, 9);
    set.priority(objHangerSplat1, 4);
    set.priority(objHangerSplat2, 4);

    if(equaln(vSplat1Cleaned, valFalse)) {
        draw(objHangerSplat1);
    }

    if(equaln(vSplat2Cleaned, valFalse)) {
        draw(objHangerSplat2);
    }
    
    draw(objBigShuttle);
    draw(ego);

    assignn(vTimer, 0);

    set.horizon(73); 

    reset(fAdrift);
    reset(fDecompressed);
    
    [ display the picture on screen
    show.pic();
}

[***************************************
[ NON-INPUT CODE 
[***************************************

if(equaln(vCleanedUp, valTrue)) {
    if(greatern(vTimer, 99)) {
        if(!isset(fWatchHasBeeped)) {
            set(fWatchHasBeeped);
            load.sound(snd.WatchBeep);
            sound(snd.WatchBeep, fSoundDone);
            print("A long beep is emitted from the watch on your arm.");    
        }
    }
 
    if(!equaln(vHasOrders, valTrue)) {
        addn(vTimer, 1);
    }
}

if(posn(ego, 85, 139, 105, 151)) {
    if(equaln(vSplat1Cleaned, valFalse)) {
        if(equaln(vSweeping, valFalse)) {
            [ DEAD: Fuel foot
            assignn(vDeathReason,  valXsFuelFoot);
            set(fDead);
        }    
        else {
          addn(vCurrentScore, 1);
          assignn(vSplat1Cleaned, valTrue);
          assignn(vCleanedUp, valTrue);
          erase(objHangerSplat1);
        }    
    }
}

if(posn(ego, 36, 80, 48, 95)) {
    if(equaln(vSplat2Cleaned, valFalse)) {
        if(equaln(vSweeping, valFalse)) {
            [ DEAD: Fuel foot
            assignn(vDeathReason, valXsFuelFoot);
            set(fDead);
        }    
        else {
            assignn(vSplat2Cleaned, valTrue);
            assignn(vCleanedUp, valTrue);
            addn(vCurrentScore, 1);
            erase(objHangerSplat2);
        }    
    }
}

[ hitbox for inner bay
if(posn(ego, 145, 135, 160, 160)) {
    if(equaln(vHasOrders, valTrue)) {
        assignn(vSweeping, valFalse);

        get.posn(ego, vEgoX, vEgoY);
        assignn(vEgoX, 15);
        assignn(vEgoY, 126);
        assignn(vRoomInited, valFalse);
        new.room(rm.InnerBay);         
    }
    else {
        print("Look at that deck!! You better keep cleaning.");
        get.posn(ego, vEgoX, vEgoY);
        assignn(vEgoX, 140);
        assignn(egoDir, LEFT);
        erase(ego);
        position.v(ego, vEgoX, vEgoY);
        draw(ego);
    }
}

if(edgeEgoHit == TOP_EDGE) {
    stop.cycling(ego);
    set.view(ego, vw.RogerspacesuitA);
    set.priority(ego, 4);
    move.obj(ego, 100, 40, 1, fAdrift);
    [ DEAD: ADRIFT 
    assignn(vDeathReason, valXsSpaceWalk);
    set(fDead);
}

if(isset(fAdrift)) {
    erase(ego);
}

if(!isset(fWearingSuit)) {
    if(!isset(fDecompressing)) {
        [ DEAD: DECOMP
        get.posn(ego, vEgoX, vEgoY);
        assignn(vEgoX, 100);
        reset(fDecompressed);
        reset(fDecompressOnce);
        assignn(v252, 1);
        move.obj.v(ego, vEgoX, vEgoY, v252, fDecompressing);
    }
}

if(isset(fDecompressing)) {
    if(!isset(fDecompressOnce)) {
        set(fDecompressOnce);
        stop.motion(ego);
        load.view(vw.RogerExplode);
        set.view(ego, vw.RogerExplode);
        end.of.loop(ego, fDecompressed);     
    }
}

if(isset(fDecompressed)) {
    erase(ego);
    assignn(vDeathReason, valXsNoSuitExplode);    
    set(fDead);
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************
if (haveInput && !haveMatch && unknownWordNum == 0) {
    if(said("stop", "sweeping")) {
        set.view(ego, vw.RogerSpacesuitA);
        assignn(vSweeping, valFalse);
    }

    if((said("start", "sweeping") || said("sweep"))) {
        if(has("Broom")) {
            set.view(ego, vw.RogerSpacesuitBroom);
            assignn(vSweeping, valTrue);
            start.cycling(ego);    
        }
        else {
            print("You don't have the proper equipment on hand for that job.");
        }
    }

    if((said("get", "pod") || said("enter", "pod"))) {
        if(posn(ego, 31, 104, 50, 127)) {
            print("Get real... This is a serious spacecraft. Access to this craft requires the appropriate orders or serious clearance -- both of which you are seriously lacking.");
        }
        else {
            print("You are too far away.");
        }
    }

    if(said("look", "watch")) {
        get.posn(ego, vEgoX, vEgoY);
        assignn(vRoomInited, valFalse);
        new.room(rm.Watch);
    }

    if(said("look", "panel")) {
        print("The panel is used to operate hanger bay equipment. It's meant to be operated by personnel assigned to flight operations which does not include janitorial staff.");
    }

    if(said("look", "space")) {
        print("Ah space... 'It's beautiful and big. Really big. You just won't believe how vastly hugely mind-bogglingly big it is.'");
    }

    if(said("look", "pod")) {
        print("Yep that's a shuttle alright. Always coming and going. You can't count the number of times you've had to clean up after the space-sick scum that frequent these shuttles. Speaking of shuttle related messes you wonder if this craft is the one that left puddles of fuel on the deck.");
    }

    if(said("look", "spill")) {
        if(equaln(vCleanedUp, valFalse)) {
            print("What a mess. There's fuel on the deck. It needs to be cleaned up.");
        }
        else {
            print("A spill?! Not on your watch! Roger Wilco. Ace Janitor!");
        }
    }

    if((said("look", "room") || said("look"))) {
        print("You are standing in a large hanger bay. The hanger door is open to the coldness of space. A shuttle sits in the middle of the bay.");
    }        
}


[***************************************
[ CHECK FOR AN EXIT CONDITION
[***************************************
:exit

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
