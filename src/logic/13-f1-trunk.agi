#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)    
    load.view(viewTrunkDoor)
    load.view(viewMist)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objTrunkDoor, viewTrunkDoor)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)

    if(!isset(fEyesOpen)) {
        set.loop(objTrunkDoor, 1)
    }

    draw(objEgo)
    draw(objTrunkDoor)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)

    position(objTrunkDoor, 100, 93)
    set.horizon(50) 

    assignn(vEgoY, 155)
    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 140)
        }
    }

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)    
    
    position(objMist, 1, 47)
    position(objMist2, 43, 47)
    position(objMist3, 85, 47)
    position(objMist4, 127, 47)

    stop.cycling(objTrunkDoor)
    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoDirection, directionDown)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }   

    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavTreeF1)   
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vEgoDirection, directionLeft)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoDirection, directionRight)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }
}


if(isset(fDoAction)) {
    reset(fDoAction)
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 20)
    assignn(vEgoY, 157)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picStairs)
} 

if(said("look")) {
    print("There's a hillside here with a door built into the side of it. There is a thick fog to the north the east and the west.")
}
if(said("knock")) {
    if(posn(objEgo, 95, 96, 118, 102)) {
        print("There is no answer.")
    }
    if(!posn(objEgo, 95, 96, 118, 102)) {
        print("You can't reach it from here.")
    }
}


if(said("look", "door")) {
    if(isset(fEyesOpen)) {
        print("The door is made of wood and looks extremely sturdy. Theres a small slot in the door")
    }
    if(!isset(fEyesOpen)) {
        print("The door is made of wood and looks extremely sturdy.")
    }
}
if(said("look", "room")) {
    print("There's a hillside here with a door built into the side of it. There is a thick fog to the north the east and the west.")
}
if(said("look", "slot")) {
    if(isset(fEyesOpen)) {
        print("On the door you find a panel with a small slot. A faint blinking red glow is emitted from somewhere inside the slot. You wonder what it could be for.")
    }
    if(!isset(fEyesOpen)) {
        print("what slot? There's no slot here.")
    }
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.")
}
if(said("insert", "keycard")) {
    if(isset(fEyesOpen)) {
        if(posn(objEgo, 95, 96, 118, 102)) {
            end.of.loop(objTrunkDoor, fDoAction)
        }
        if(!posn(objEgo, 95, 96, 118, 102)) {
            print("You can't reach it from here.")
        }        
    }
    if(!isset(fEyesOpen)) {
        print("You can't do that.")
    }
}

return()
#message 1 "Trunk Door"