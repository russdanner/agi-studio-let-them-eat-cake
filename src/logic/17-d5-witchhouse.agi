#include "0-defines.aqi"

if(equaln(vRoomInited, 0)) {
    unanimate.all()
    assignn(vRoomInited, 1)

    load.view(viewRoger)
    load.view(viewRogerSwim)
    load.view(viewMist)
    load.view(viewWitchDoor)
   
    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)
    set.view(objWitchDoor, viewWitchDoor)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)
    stop.cycling(objWitchDoor)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)
    draw(objWitchDoor)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 140)
            assignn(vEgoY, 130)
        }
    }

    reset(fDoAction)

    position(objMist, 1, 166)
    position(objMist2, 43, 166)
    position(objMist3, 85, 166)
    position(objMist4, 127, 166)
    position(objWitchDoor, 45, 129)

    position.v(objEgo, vEgoX, vEgoY)
    assignn(vEgoEdgeTouched, edgeNone)
    set.horizon(50)
    set.priority(objWitchDoor, 10)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 155)
        assignn(vEgoDirection, directionUp)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }   

    if(equaln(vEgoEdgeTouched, edgeTop)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavLakeD4)   
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWizardE5)     
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 15)
        assignn(vEgoDirection, directionRight)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}


if(isset(fDoAction)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 50)
    assignn(vEgoY, 150)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picDavWitchHouseInside)
}

if(said("knock")) {
    if(posn(objEgo, 38, 128, 56, 135)) {
        print("There is no answer.")
    }
    if(!posn(objEgo, 38, 128, 56, 135)) {
        print("You can't reach it from here.")
    }
}
if(said("look")) {
    print("There's a house nestled in to a heavily wooded area. It seems to be made of gingerbread and candy. A thick flog extends to the west and south.")
}
if(said("look", "room")) {
    print("There's a house nestled in to a heavily wooded area. It seems to be made of gingerbread and candy. A thick flog extends to the west and south.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.")
}
if(said("look", "house")) {
    print("You are not quite sure what to make of it. It's a house made out of candy. It looks... sticky. As a janitor sticky bothers you.")
}
if(said("lick", "house")) {
    print("You give the house a lick.")
}
if(said("open", "door")) {
    if(posn(objEgo, 38, 128, 56, 135)) {
        end.of.loop(objWitchDoor, fDoAction)
    }
    if(!posn(objEgo, 38, 128, 56, 135)) {
        print("You can't reach it from here.")
    }
}

// have to enable sound off because of the 3 word checks
// these comon things need to be built into a better inc stem
if(said("fastest", "speed")) {
    assignn(10, 1)
}
if(said("fast", "speed")) {
    assignn(10, 2)
}
if(said("normal", "speed")) {
    assignn(10, 3)
}
if(said("slow", "speed")) {
    assignn(10, 5)
}
if(said("sound", "on")) {
    set(fSysSoundOn)
}
if(said("sound", "off")) {
    stop.sound()
    reset(fSysSoundOn)
}
if(said("look", "anyword")) {
        print("It's nothing special or important.")
}
if(said("lick", "cake")) {
    if(has(invCake)) {
        print("Party foul.")
    }
    if(!has(invCake)) {
        print("If you had cake you could try that.")
    }    
}

if(said("anyword", "anyword")) {
    print("You can't do that. At least not now.")
}

if(said("knock", "on", "door")) {
    if(posn(objEgo, 38, 128, 56, 135)) {
        print("There is no answer.")
    }
    if(!posn(objEgo, 38, 128, 56, 135)) {
        print("You can't reach it from here.")
    }
}

return()
#message 1 "Whitch House"
