#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewTram)
    load.view(viewLift)

    animate.obj(objEgo)
    animate.obj(objLiftRod)
    animate.obj(objCrew1)
    animate.obj(objCrew2)

    set.view(objEgo, viewRoger)
    set.view(objTram, viewTram)
    set.view(objLift, viewLift)
    set.view(objLiftRod, viewLift)
    set.view(objCrew1, viewLift)
    set.view(objCrew2, viewLift)

    draw(objTram)
    draw(objLift)
    draw(objLiftRod)
    draw(objCrew1)
    draw(objCrew2)

    stop.cycling(objTram)
    stop.cycling(objLift)
    stop.cycling(objLiftRod)

    set.loop(objLiftRod, 1)
    set.cel(objLiftRod, 0)
    set.loop(objCrew1, 2)
    set.loop(objCrew2, 3)

    position(objLift, 21, 92)
    position(objLiftRod, 34, 130)
    position(objCrew1, 70, 110)
    position(objCrew2, 100, 110)

    set.priority(objTram, 4)
    set.priority(objLift, 4)
    set.priority(objLiftRod, 4)
    set.priority(objCrew1, 4)
    set.priority(objCrew2, 4)

    block(68, 90, 78, 112)
    block(98, 90, 108, 112)

    set(fLiftGoingDown)


    reset(fOnLift)
    reset(fTramReady)
    reset(fTramRunning)
    reset(fTramInboundDone)
    reset(fTramOutboundDone)
    reset(fHasFlightPlan)

    ignore.blocks(objTram)
    position(objTram, 5, 72)
    move.obj(objTram, 95, 72, 2, fTramInboundDone)
    load.sound(sndTramStop)
    sound(sndTramStop, fSoundDone)
}

addn(vTimer, 1)

if(!isset(fLiftRunning)) {
    if(equaln(vEgoDirection, directionStopped)) {
        stop.cycling(objEgo)
    }
    if(!equaln(vEgoDirection, directionStopped)) {
        start.cycling(objEgo)
    }    
}
if(isset(fLiftRunning)) {
    // poor mans program control
    assignn(vEgoDirection, directionStopped)
}

if(said("look")) {
    print("This is the main control room for the station. It's alive with activity. To the rear wall there is a pneumatic transport tube and car. The StarCon Control personnel sit on the first level coordinating flight activities and tram movements within the station.")
}

if(said("get", "tram")) {    
    if(isset(fHasFlightPlan)) {
        if(posn(objEgo, 91, 73, 109, 76)) {
            erase(objEgo)
            end.of.loop(objTram, fTramReady) 
            set(fTramRunning)       
    
            load.sound(sndTramGo)
            sound(sndTramGo, fSoundDone)        
        }
        if(!posn(objEgo, 91, 73, 109, 76)) {
            print("You are not close enough.")
        }
    }
    if(!isset(fHasFlightPlan)) {
        position(objEgo, 95, 74)
        set.cel(objTram, 0)
        draw(objEgo)
    }
}

if(said("exit", "tram")) {    
    position(objEgo, 95, 74)
    set.cel(objTram, 0)
    draw(objEgo)
}

if(said("enter", "tram")) {
    if(isset(fHasFlightPlan)) {
        if(posn(objEgo, 91, 73, 109, 76)) {
            erase(objEgo)
            end.of.loop(objTram, fTramReady) 
            set(fTramRunning)       
    
            load.sound(sndTramGo)
            sound(sndTramGo, fSoundDone)        
        }
        if(!posn(objEgo, 91, 73, 109, 76)) {
            print("You are not close enough.")
        }
    }
    if(!isset(fHasFlightPlan)) {
        print("You better speak with control first.")
    }
}

if(obj.in.box(objEgo, 24, 86, 34, 89)) {
    // going down
    if(!isset(fOnLift)) {
        set(fOnLift)
        set(fLiftRunning)
        set(fLiftGoingDown)
        assignn(vTimer, 0)

        assignn(vEgoDirection, directionStopped)
    }
}
if(obj.in.box(objEgo, 24, 114, 34, 123)) {
    // going up
    if(!isset(fOnLift)) {
        set(fOnLift)
        set(fLiftRunning)
        assignn(vTimer, 0)
        reset(fLiftGoingDown)
    }
}
if(!obj.in.box(objEgo, 24, 86, 34, 123)) {
    // roger stepped off the list
    reset(fOnLift)
    reset(fLiftRunning)
}

if(isset(fLiftRunning)) {
    // this needs to be looked at
    // stop.motion(objEgo)
    // program.control()

    if(isset(fLiftGoingDown)) {
        // render down
        reposition(objLift, 0, 1)
        reposition(objLiftRod, 0, 1)
        reposition(objEgo, 0, 1)    
    }
    if(!isset(fLiftGoingDown)) {
        // bug with -1
        //reposition(objLift, 0, -1)
        //reposition(objLiftRod, 0, -1)
        //reposition(objEgo, 0, -1)
        position(objEgo, 33, 75)  
        position(objLift, 21, 92)
        position(objLiftRod, 34, 130)
    }

    if(equaln(vTimer, 26)) {
        reset(fLiftRunning)
        //player.control()
    }
}

if(isset(fTramReady)) {
    if(isset(fHasFlightPlan)) {
        reset(fTramReady)
        move.obj(objTram, 160, 72, 2, fTramOutboundDone)    
    }
}

if(isset(fTramOutboundDone)) {
    reset(fTramOutboundDone)
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picOuterBay)     
}

if(isset(fTramInboundDone)) {
    reset(fTramInboundDone)
    position(objTram, 94, 72)
    set.cel(objTram, 1)
}


if(said("talk", "man")) {
    if(posn(objEgo, 94, 114, 116, 125)) {
        if(isset(fHasFlightPlan)) {
            print("You better get moving Roger. I've got a lot on my plate here.")
        }
        if(!isset(fHasFlightPlan)) {
            set(fHasFlightPlan)
            addn(vCurrentScore, 1)
            assignn(vTimer, 0)
            print("Hey Roger. I'm sending you over to Hanger B for departure and transit to Orbital Station 3 per the Commander. A pod has been prepared and is standing by. Before you go... You know.. Swapping out my cave ball whistle for a Labion terror beast mating whistle was not funny... I mean... just look at these marks! I'll get you back for that at some point... Better watch it.")
        }
    }
    // need better control structures from the compiler
    if(posn(objEgo, 53, 114, 80, 125)) {
            print("Can't you see I am busy here?")
    }
    if(!posn(objEgo, 94, 114, 116, 125)) {
        if(!posn(objEgo, 53, 114, 80, 125)) { 
            print("Who?")
        }
    }
}

if(said("look", "room")) {
    print("This is the main control room for the station. It's alive with activity. To the rear wall there is a pneumatic transport tube and car. The StarCon Control personnel sit on the first level coordinating flight activities and tram movements within the station.")
}
if(said("look", "man")) {
    print("Keep your eyes to yourself Roger.")
}
if(said("look", "tram")) {
    print("The pneumatic transport tube enables personnel to move about the station. It's destinations are managed by control. #tramIt")
}

return()

#message 1 "Control Room"
