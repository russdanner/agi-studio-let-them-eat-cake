#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    // debugging scenario: Get your orders with this flag high
    // assignn(vEgoX, 55)
    // assignn(vEgoY, 55)
    
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewRogerSuitA)
    load.view(viewCloset)
    load.view(viewTram)
    load.view(viewBathroomDoor)
    load.view(viewHangerDoorLeft)

    animate.obj(objEgo)

    set.view(objTram, viewTram)
    set.view(objCloset, viewCloset)
    set.view(objHangingSuit1, viewRogerSuitA)
    set.view(objHangingSuit2, viewRogerSuitA)
    set.view(objBathroomDoor, viewBathroomDoor)
    set.view(objBathroomDoor1, viewBathroomDoor)
    set.view(objHangerDoorLeft, viewHangerDoorLeft)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 10)
            assignn(vEgoY, 126)
            set(fWearingSuit)
        }
    }

    if(isset(fWearingSuit)) {
        set.view(objEgo, viewRogerSuitA)
    }
    if(!isset(fWearingSuit)) {
        set.view(objEgo, viewRoger)
        draw(objHangingSuit2)
        position(objHangingSuit2, 43, 95)    
    }

    draw(objEgo)
    draw(objCloset)
    draw(objTram)
    draw(objHangingSuit1)
    draw(objBathroomDoor)
    draw(objBathroomDoor1)
    draw(objHangerDoorLeft)

    stop.cycling(objCloset)
    stop.cycling(objTram)
    stop.cycling(objHangingSuit1)
    stop.cycling(objHangingSuit2)
    stop.cycling(objBathroomDoor)
    stop.cycling(objBathroomDoor1)
    stop.cycling(objHangerDoorLeft)

    set.loop(objBathroomDoor1, 1)
    set.loop(objHangingSuit1, 2)
    set.loop(objHangingSuit2, 2)

    position(objCloset, 100, 101)
    position(objTram, 55, 47)
    position(objBathroomDoor, 60, 98)
    position(objBathroomDoor1, 67, 98)
    position(objHangingSuit1, 35, 95)
    position(objHangerDoorLeft, 6, 131)
   
    set.priority(objEgo, 13)

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)

    reset(fDoAction)
}

addn(vTimer, 1)

if(obj.in.box(objEgo, 100, 95, 113, 110)) {
    end.of.loop(objCloset, fDoor2Open)

    if(!isset(fDoor2Open)) {
        load.sound(sndSpaceDoor)
        sound(sndSpaceDoor, fSoundDone)        
    }
}
if(!obj.in.box(objEgo, 100, 95, 113, 110)) {
    if(isset(fDoor2Open)) {
        set.cel(objCloset, 0)
        load.sound(sndSpaceDoor)
        sound(sndSpaceDoor, fSoundDone)    
        reset(fDoor2Open)
        reset(fDoorOpen)
    }
}

if(posn(objEgo, 8, 123, 20, 128)) {
    end.of.loop(objHangerDoorLeft, fDoorOpen)

    if(!isset(fDoorOpen)) {
        load.sound(sndSpaceDoor)
        sound(sndSpaceDoor, fSoundDone)        
    }
    reset(fDoorOpen)
}
if(posn(objEgo, 21, 123, 25, 128)) {
    set.cel(objHangerDoorLeft, 0)
    //end.of.loop(objHangerDoorLeft, 99)
    if(isset(fDoorOpen)) {
        load.sound(sndSpaceDoor)
        sound(sndSpaceDoor, fSoundDone)        
    }
    reset(fDoorOpen)
}

if(obj.in.box(objEgo, 2, 123, 5, 128)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 146)
    assignn(vEgoY, 141)
    assignn(vRoomInited, valFalse)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vCurrentRoom, picHanger)     
}

if(said("undress")) {
    print("I'll get naked if you get naked. You go first.")
}

if(said("look")) {
    print("This is Inner Bay A of the station. There are currently no ships in this bay and it is sealed and pressurized. Along the rear wall you see a bathroom a closet and a pneumatic transport tube and car.")
}

if(said("sleep")) {
    if(posn(objEgo, 100, 95, 120, 103)) {
        print("You have to admit this closet looks like a perfect place for a nap. You also have to admit that you had a similar thought earlier today. Given that you are running behind you decide with a degree of sadness NOT to take another nap.")
    }
    if(!posn(objEgo, 100, 95, 120, 103)) {
        print("You can't do that here. Perhaps a comfy janitorial closet would be a more suitable.")
    }
}

if(said("sweep")) {
    if(has(invBroom)) {
        print("With this broom?! ROGER! This is a polished floor not some crummy hanger deck. You know better. Ace janitor indeed.")
    }
    if(!has(invBroom)) {
        print("You don't have the proper equipment on hand for that.")
    }
}

if(said("start", "sweeping")) {
    if(has(invBroom)) {
        print("With this broom?! ROGER! This is a polished floor not some crummy hanger deck. You know better. Ace janitor indeed.")
    }
    if(!has(invBroom)) {
        print("You don't have the proper equipment on hand for that.")
    }
}

if(said("push", "button")) {
    if(isset(fWearingSuit)) {
        print("You can't go in there wearing your EVA pressure suit. That could get VERY messy.")
    }
    if(!isset(fWearingSuit)) {
        if(posn(objEgo, 71, 100, 85, 105)) {
            load.sound(sndSpaceDoor)
            sound(sndSpaceDoor, fSoundDone)    
        
            get.posn(objEgo, vEgoX, vEgoY)
            assignn(vEgoX, 75)
            assignn(vEgoY, 148)
            assignn(vEgoEdgeTouched, edgeNone)
            assignn(vRoomInited, valFalse)
            assignn(vCurrentRoom, picBathroom)
        }
        if(!posn(objEgo, 71, 100, 85, 105)) {
            print("Which one?")
        }
    }
}

if(said("take", "suit")) {
    if(isset(fWearingSuit)) {
        print("You are already wearing it.")
    }
    if(!isset(fWearingSuit)) {
        if(posn(objEgo, 35, 100, 50, 105)) {
            set.view(objEgo, viewRogerSuitA)
            erase(objHangingSuit2)
            set(fWearingSuit)
            print("Ok")
        }
        if(!posn(objEgo, 35, 100, 50, 105)) {
            print("You are not close enough.")
        }
    }
}

if(said("wear", "suit")) {
    if(isset(fWearingSuit)) {
        print("You are already wearing it.")
    }
    if(!isset(fWearingSuit)) {
        if(posn(objEgo, 35, 100, 50, 105)) {
            set.view(objEgo, viewRogerSuitA)
            erase(objHangingSuit2)
            set(fWearingSuit)
            print("Ok")
        }
        if(!posn(objEgo, 35, 100, 50, 105)) {
            print("You are not close enough.")
        }
    }
}

if(said("remove", "suit")) {
    if(!isset(fWearingSuit)) {
        print("You are not wearing it.")
    }
    if(isset(fWearingSuit)) {
        if(posn(objEgo, 35, 100, 50, 105)) {
            if(!isset(fPointsForRemoveSuit)) {
                set(fPointsForRemoveSuit)
                addn(vCurPoints, 1)
            }

            load.view(viewRoger)
            set.view(objEgo, viewRoger)
            draw(objHangingSuit2)
            position(objHangingSuit2, 43, 95)
            reset(fWearingSuit)        
        }
        if(!posn(objEgo, 35, 100, 50, 105)) {
            print("You are not close enough.")
        }
    }
}

if(said("enter", "tram")) {
    if(isset(fWearingSuit)) {
        print("You can't get in the pneumatic transport tube car in your EVA pressure suit.")
    }
    if(!isset(fWearingSuit)) {
        if(posn(objEgo, 56, 45, 68, 51)) {
            erase(objEgo)
            end.of.loop(objTram, fDoAction)
        }
        if(!posn(objEgo, 56, 45, 68, 51)) {
            print("Get closer.")
        }
    }
}
if(said("get", "tram")) {
    if(isset(fWearingSuit)) {
        print("You can't get in the pneumatic transport tube car in your EVA pressure suit.")
    }
    if(!isset(fWearingSuit)) {
        if(posn(objEgo, 56, 45, 68, 51)) {
            erase(objEgo)
            end.of.loop(objTram, fDoAction)
        }
        if(!posn(objEgo, 56, 45, 68, 51)) {
            print("Get closer.")
        }
    }
}

if(isset(fDoAction)) {
    reset(fDoAction)
    reset(fTramOutboundDone)
    assignn(vTimer, 0)

    set(fTramRunning)     
    load.sound(sndTramGo)
    sound(sndTramGo, fSoundDone)
    ignore.blocks(objTram)    
    move.obj(objTram, 150, 47, 2, fTramOutboundDone)        
}

if(isset(fTramOutboundDone)) {
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picControlRoom)    
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("drop", "broom")) {
    if(posn(objEgo, 100, 95, 120, 103)) {
        if(!has(invBroom)) {
            print("You don't have one.")
        }
        if(has(invBroom)) {
            addn(vCurrentScore, 1)
            drop(invBroom)
            print("Ok")    
        }
    }
}
if(said("take", "broom")) {
    if(!has(invBroom)) {
        get(invBroom)
        subn(vCurrentScore, 1)
        print("Ok")
    }

}

if(said("look", "room")) {
    print("This is Inner Bay A of the station. There are currently no ships in this bay and it is sealed and pressurized. Along the rear wall you see a bathroom a closet and a pneumatic transport tube and car.")
}
if(said("look", "bathroom")) {
    print("'Look' is really not really the verb you would prefer to associate with StarCon bathrooms. It appears to be your standard issue StarCon facilities. There is a button by the door and a small display which indicates that the room is currently unoccupied.")
}
if(said("look", "closet")) {
    print("This is a janitorial supply closet. It sure looks comfy... I mean... like the pinnacle of janitorial supply storage solutions.")
}
if(said("look", "suit")) {
    print("Good eye. There is a EVA pressure suit rack here.")
}
if(said("look", "tram")) {
    print("The pneumatic transport tube is used to move about the various areas within the station. The car is currently waiting in this location.")
}

return()

#message 1 "Inner Bay"