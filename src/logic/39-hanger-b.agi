#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)    
    load.view(viewRogerSuitA)
    load.view(viewPodPlatform)
    load.view(viewPod)

    animate.obj(objEgo)

    set.view(objPod, viewPod)
    set.view(objPodPlatform, viewPodPlatform)

    start.cycling(objEgo)
    stop.cycling(objPodPlatform)
    stop.cycling(objPod)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            set(fWearingSuit)
        }
    }

    assignn(vEgoX, 150)
    assignn(vEgoY, 144)

    if(isset(fWearingSuit)) {
        set.view(objEgo, viewRogerSuitA)
    }
    if(!isset(fWearingSuit)) {
        set.view(objEgo, viewRoger)
    }

    draw(objEgo)
    draw(objPodPlatform)
    draw(objPod)

    set.priority(objPod, 12)
    position(objPodPlatform, 55, 109)
    position(objPod, 60, 100)
    set.horizon(73) 

    position.v(objEgo, vEgoX, vEgoY)    
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("look")) {
    print("You are standing in a large hangar bay. The hangar door is open to the coldness of space. A shuttle sits in the middle of the bay.")
}

if(said("get", "pod")) {
    if(posn(objEgo, 49, 93, 60, 109)) {
        addn(vCurrentScore, 1)

        reset(fLaunchAnimationPlayed)

        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picInPod)    

        if(!has(invBroom)) {
            print("You climb into the pod and shut the door.")
        }
        if(has(invBroom)) {
            drop(invBroom)
            print("As you go to climb into the pod you realize you still have a broom with you. Since you are in a hurry you discard the broom here. You'll catch some grief for that. Broomless you now climb into the pod and shut the door.")
        }        
    }
    if(!posn(objEgo, 49, 93, 60, 109)) {
        print("You are too far away.")
    }
}

if(said("enter", "pod")) {
    if(posn(objEgo, 39, 93, 60, 109)) {
        addn(vCurrentScore, 1)

        reset(fLaunchAnimationPlayed)

        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picInPod)    

        if(!has(invBroom)) {
            print("You climb into the pod and shut the door.")
        }
        if(has(invBroom)) {
            drop(invBroom)
            print("As you go to climb into the pod you realize you still have a broom with you. Since you are in a hurry you discard the broom here. You'll catch some grief for that. Broomless you now climb into the pod and shut the door.")
        }        
    }
    if(!posn(objEgo, 39, 93, 60, 109)) {
        print("You are too far away.")
    }
}

if(obj.in.box(objEgo, 151, 140, 155, 150)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 10)
    assignn(vEgoY, 126)
    assignn(vRoomInited, valFalse)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vCurrentRoom, picOuterBay)         
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        stop.cycling(objEgo)
        set.view(objEgo, viewRogerSuitA)
        set.priority(objEgo, 4)
        move.obj(objEgo, 100, 40, 1, fAdrift)
        // DEAD: ADRIFT 
        assignn(vDeathReason, valXsSpaceWalk)
        set(fDead)
    }
}
if(isset(fAdrift)) {
    erase(objEgo)
}

if(!isset(fWearingSuit)) {
    if(!isset(fDecompressing)) {
        // DEAD: DECOMP
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 100)
        reset(fDecompressed)
        reset(fDecompressOnce)
        move.obj.v(objEgo, vEgoX, vEgoY, 1, fDecompressing)
    }
}

if(isset(fDecompressing)) {
    if(!isset(fDecompressOnce)) {
        set(fDecompressOnce)
        stop.motion(objEgo)
        load.view(viewRogerExplode)
        set.view(objEgo, viewRogerExplode)
        end.of.loop(objEgo, fDecompressed)     
    }
}

if(isset(fDecompressed)) {
    erase(objEgo)
    assignn(vDeathReason, valXsNoSuitExplode)    
    set(fDead)
}

if(said("look")) {
    print("You are standing in a large hangar bay. The hangar door is open to the coldness of space. A shuttle sits in the middle of the bay.")
}
if(said("look", "room")) {
    print("You are standing in a large hangar bay. The hangar door is open to the coldness of space. A shuttle sits in the middle of the bay.")
}
if(said("look", "panel")) {
    print("The panel is used to operate hangar bay equipment. It's meant to be operated by personnel assigned to flight operations which does not include janitorial staff.")
}
if(said("look", "space")) {
    print("Ah space... 'It's beautiful and big. Really big. You just won't believe how vastly hugely mind-bogglingly big it is.'")
}
if(said("look", "pod")) {
    print("Ain't she cute? It's a standard issue StarCon shuttle pod used for short trips between stations and bases. In the extremely rare and unlikely event of an emergency including but not limited to a hostile encounter with a crew of marauding aliens it can be used as an escape vehicle. That'll never happen. 'Anywho'.. She's all fueled up and ready to go.")
}

return()
#message 1 "Hanger Bay 2"