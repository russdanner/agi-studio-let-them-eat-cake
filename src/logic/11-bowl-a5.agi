#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()
    assignn(vRoomInited, valTrue)

    // testing
    // get(invCake)
    // get(invInk)
    // get(invGlass)
    // get(invPaper)
    // get(invQuill)
    // get(invFeather)

    load.view(viewRoger)
    load.view(viewRogerSplash)
    load.view(viewRogerSwim)    
    load.view(viewMist)
    load.view(viewHawk)
    load.view(viewFeatherFall)

    animate.obj(objEgo)
    animate.obj(objHawk)
    animate.obj(objFeatherFall)

    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)
    set.view(objHawk, viewHawk)
    set.view(objFeatherFall, viewFeatherFall)


    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)
    draw(objHawk)
    draw(objFeatherFall)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 140)
            assignn(vEgoY, 70)
        }
    }

    position(objMist, 1, 166)
    position(objMist2, 43, 166)
    position(objMist3, 85, 166)
    position(objMist4, 127, 166)

    reset(fHawkFlightADone)
    reset(fHawkFlightBDone)
    reset(fFeatherFakllDone)

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)
    set.horizon(50)

    if(!has(invFeather)) {
        if(!has(invQuill)) {
            if(has(invCake)) {
                if(has(invInk)) {
                    set.priority(objHawk, 10)    
                    position(objHawk, 160, 70)
                    move.obj(objHawk, 40, 40, 2, fHawkFlightADone)                
                }
            }
        }
    }
}


if(isset(fHawkFlightADone)) {
    reset(fHawkFlightADone)
    position(objFeatherFall, 40, 40)
    move.obj(objHawk, 0, 40, 1, fHawkFlightBDone)
    move.obj(objFeatherFall, 40, 105, 1, fFeatherFakllDone)
}

if(isset(fFeatherFakllDone)) {
    reset(fFeatherFakllDone)
    stop.cycling(objFeatherFall)
}

if(isset(fHawkFlightBDone)) {
    erase(objHawk)
}

if(said("take", "feather")) {
    if(!has(invFeather)) {
        if(!has(invQuill)) {
            if(posn(objEgo, 40, 101, 52, 107)) {
                get(invFeather)
                print("You reach down and pick up the fallen feather.")
                addn(vCurPoints, 1)    
                erase(objFeatherFall)      
            } 
            if(!posn(objEgo, 40, 101, 52, 107)) {
                print("You can't reach it from here.")
            }
        }
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 155)
        assignn(vEgoDirection, directionUp)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)           
    }   

    if(equaln(vEgoEdgeTouched, edgeTop)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavRiverBendA4)     
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vEgoDirection, directionLeft)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWalnutF5)    
    }
}

if(said("look", "room")) {
    print("This is a forrested and rocky area. A think fog has developed to the south and the east.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. To more you look into it the most lost you feel.")
}

return()
#message 1 "Bowl"