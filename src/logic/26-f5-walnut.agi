#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    // testing
    // get(invGlass)
    // get(invFeather)

    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewRogerSwim)
    load.view(viewMist)
   
    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 100)
            assignn(vEgoY, 110)
        }
    }

    position(objMist, 1, 166)
    position(objMist2, 43, 166)
    position(objMist3, 85, 166)
    position(objMist4, 127, 166)

    position.v(objEgo, vEgoX, vEgoY)
    assignn(vEgoEdgeTouched, edgeNone)
    set.horizon(50)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 155)
        assignn(vEgoDirection, directionUp)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }   

    if(equaln(vEgoEdgeTouched, edgeTop)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavDwarfF4)   
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavBowlA5)     
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWizardE5)     
    }        
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("look")) {
    print("The ground is covered in walnuts. They have fallen from a beautiful walnut tree that towers into the sky. A pointy rock juts up from the ground by the tree. A thick fog rolls in from the south.")
}
if(said("look", "room")) {
    print("The ground is covered in walnuts. They have fallen from a beautiful walnut tree that towers into the sky. A pointy rock juts up from the ground by the tree. A thick fog rolls in from the south.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.")
}
if(said("look", "rock")) {
    print("There's a rock here that that is very pointed. It has several facets on it. Some of them are rough and others are smooth.")
}
if(said("look", "walnut")) {
    print("The ground is covered with walnuts that have fallen from the tree.")
}
if(said("take", "walnut")) {
    print("You don't need it.")
}

if(said("sharpen", "glass")) {
    if(posn(objEgo, 90, 115, 125, 130)) {
        if(isset(fGlassIsSharp)) {
            // should probably kill roger here
            print("Sheesh Roger... the glass is as sharp as it's going to get.")
        }
        if(!isset(fGlassIsSharp)) {
            set(fGlassIsSharp)
            addn(vCurPoints, 1)
            print("You sharpen the highly reflective safety glass by rubbing the edge of the glass repeatedly against the rock. Be careful with that thing Roger.")    
        }
    }
    if(!posn(objEgo, 90, 115, 125, 130)) {
        print("How?")
    }
}

if(said("sharpen", "glass", "rock")) {
    if(posn(objEgo, 90, 115, 125, 130)) {
        if(isset(fGlassIsSharp)) {
            // should probably kill roger here
            print("Sheesh Roger... the glass is as sharp as it's going to get.")
        }
        if(!isset(fGlassIsSharp)) {
            set(fGlassIsSharp)
            addn(vCurPoints, 1)
            print("You sharpen the highly reflective safety glass by rubbing the edge of the glass repeatedly against the rock. Be careful with that thing Roger.")    
        }
    }
    if(!posn(objEgo, 90, 115, 125, 130)) {
        print("How?")
    }
}

return()
#message 1 "Walnuts"
