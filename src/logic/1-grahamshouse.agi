#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewGrahamNoHat)
    load.view(viewGrahamHatNoFeather)
    load.view(viewSummons)
    load.view(viewHat)
    load.view(viewTeapot)
    load.view(viewTeaCup)
    load.view(viewGrahamSleep)
    load.view(viewWindow)
    load.view(viewCabinDoor)

    animate.obj(objEgo)
    animate.obj(objTeapot)

    set.view(objEgo, viewGrahamNoHat)
    set.view(objSummons, viewSummons)
    set.view(objHat, viewHat)
    set.view(objTeapot, viewTeapot)
    set.view(objTeaCup, viewTeaCup)
    set.view(objGrahamSleep, viewGrahamSleep)
    set.view(objWindow, viewWindow)
    set.view(objCabinDoor, viewCabinDoor)

    stop.cycling(objSummons)
    stop.cycling(objHat)
    stop.cycling(objTeaCup)
    stop.cycling(objWindow)
    stop.cycling(objCabinDoor)

    draw(objEgo)
    draw(objSummons)
    draw(objHat)
    draw(objTeapot)
    draw(objTeaCup)
    draw(objWindow)
    draw(objCabinDoor)

    assignn(vSleeping, valFalse)
    assignn(vClockTicks, 0)
    
    if(isset(fIsMorning)) {
        // switching from roger back to graham
        drop(invKeycard)
        drop(invGlass)
        drop(invCake)
        drop(invInk)
        drop(invQuill)
        drop(invFeather)
        drop(invPaper)
        drop(invInvitation)

        drop(invTea)
        get(invTeaCup)
        get(invTeapot)

        if(isset(fHasHat)) {
            get(invHat)
        }        
        if(isset(fHasSummons)) {
            get(invSummons)
        }        
    }

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            position(objEgo, 100, 130)
            if(!isset(fIsMorning)) {
                assignn(vTeaDrank, valFalse)
                print("It is getting late and you should go to bed but you cannot sleep. Earlier today you received a summons to appear at the castle to meet with King Edward for reasons unknown.")
            }        
        }
    }

    if(!equaln(vEgoX, 0)) {
        position.v(objEgo, vEgoX, vEgoY)
    }

    position(objWindow, 90, 58)

    if(!has(invHat)) {
        position(objHat, 130, 125)
    }
    if(!has(invTeapot)) {
        position(objTeapot, 110, 87)
    }
    if(!has(invTea)) {
        if(!has(invTeaCup)) {
            position(objTeaCup, 55, 105)
        }
    }
    if(!has(invSummons)) {
        position(objSummons, 80, 105)
    }

    set.priority(objSummons, 15)
    set.priority(objHat, 15)
    set.priority(objTeapot, 16)
    set.priority(objTeaCup, 15)

    if(isset(fIsMorning)) {
        set.cel(objWindow, 1)
        position(objEgo, 100,80) 
        print("Thinking of the King you are jarred awake. It's morning. You must get to the castle to meet with King Edward!")
    }
    set.cel(objCabinDoor, 1)
    position(objCabinDoor, 26, 131)
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
 }
 if(!equaln(vEgoDirection, directionStopped)) {
     start.cycling(objEgo)
 }
 
if(said("look")) {
    if(!has(invHat)) {
        print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter with a hat on it in the corner.")
    }
    if(has(invHat)) {
        print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter in the corner.")
    }
}

if(said("sleep")) {
    if(posn(objEgo, 75, 30, 115, 85)) {
        if(!equaln(vTeaDrank, valTrue)) {
            print("You are a bit on edge about the summons you received from the king. You can't sleep.")
        }
        if(equaln(vTeaDrank, valTrue)) {
            assignn(vSleeping, valTrue)
            assignn(vTimer, 0)
            assignn(vClockTicks, 0)
            addn(vCurrentScore, 1)    
            print("Having taken the tea you climb into bed and doze off.")
        }
    }
    if(!posn(objEgo, 75, 30, 115, 85)) {
        print("You're not close enough.")
    }
}

if(said("go", "sleep")) {
    if(posn(objEgo, 75, 30, 115, 85)) {
        if(!equaln(vTeaDrank, valTrue)) {
            print("You are a bit on edge about the summons you received from the king. You can't sleep.")
        }
        if(equaln(vTeaDrank, valTrue)) {
            assignn(vSleeping, valTrue)
            assignn(vTimer, 0)
            assignn(vClockTicks, 0)
            addn(vCurrentScore, 1)    
            print("Having taken the tea you climb into bed and doze off.")
        }
    }
    if(!posn(objEgo, 75, 30, 115, 85)) {
        print("You're not close enough.")
    }
}

if(said("look", "room")) {
    if(!has(invHat)) {
        print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter with a hat on it in the corner.")
    }
    if(has(invHat)) {
        print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter in the corner.")
    }
}
if(said("look", "stove")) {
    if(has(invTeapot)) {
        print("The stove is made of cast iron and keeps the room toasty.")
    }
    if(!has(invTeapot)) {
        print("The stove is made of cast iron and keeps the room toasty. There's a teapot on top of the stove.")
    }
}

if(said("look", "table")) {
    if(has(invSummons)) {
        print("It's a sturdy table.")
    }
    if(!has(invSummons)) {
        print("There is a study table here. The summons you received from the King today lays on the table.")
    }
}

if(said("open", "door")) {
    if(posn(objEgo, 16, 108, 35, 124)) {
        if(!has(invHat)) {
            print("You would never leave the house without your hat.")
        }
        if(has(invHat)) {
            if(isset(fIsMorning)) {
                addn(vCurrentScore, 1)    
                get.posn(objEgo, vEgoX, vEgoY)
                assignn(vEgoX, 119)
                assignn(vEgoY, 126)
                assignn(vRoomInited, valFalse)
                assignn(vCurrentRoom, picGrahamLawn)     
            }
            if(!isset(fIsMorning)) {
                get.posn(objEgo, vEgoX, vEgoY)
                assignn(vEgoX, 119)
                assignn(vEgoY, 126)
                assignn(vRoomInited, valFalse)
                assignn(vCurrentRoom, picGrahamLawnNight)     
            }
        }
    }
    if(!posn(objEgo, 16, 108, 35, 124)) {
        print("You can't do that from here.")
    }
}

if(equaln(vSleeping, valTrue)) {
    erase(objEgo)
    stop.cycling(objGrahamSleep)
    draw(objGrahamSleep)
    position(objGrahamSleep, 110, 82)
    set.priority(objGrahamSleep, 15)
}

if(said("wear", "hat")) {
    if(has(invHat)) {
        if(isset(fWearingHat)) {
            print("You are already wearing it.")
        }
        if(!isset(fWearingHat)) {
            set(fWearingHat)
            set.view(objEgo, viewGrahamHatNoFeather)
            print("You put on your trusty hat!")

            if(!isset(fPointsForHatWear)) {
                set(fPointsForHatWear)
                addn(vCurrentScore, 1)    
            }
        }
    }
    if(!has(invHat)) {
        print("You don't have it.")
    }
}   

if(said("remove", "hat")) {
    if(has(invHat)) {
        if(!isset(fWearingHat)) {
            print("You are not wearing your hat.")
        }
        if(isset(fWearingHat)) {
            reset(fWearingHat)
            set.view(objEgo, viewGrahamNoHat)
        }
    }
    if(!has(invHat)) {
        print("You don't have it.")
    }
}  

if(said("take", "summons")) {
    if(posn(objEgo, 80, 100, 100, 130)) {
        if(has(invSummons)) {
            print("You already have it.")
        }
        if(!has(invSummons)) {
            print("You take the summons.")
            set(fHasSummons)
            get(invSummons)
            erase(objSummons)
            addn(vCurrentScore, 1)        
        }
    }
    if(!posn(objEgo, 80, 100, 100, 130)) {
        print("Get closer.")
    }
}

if(said("take", "teapot")) {
    if(posn(objEgo, 104, 93, 123, 112)) {
        if(has(invTeapot)) {
            print("You alread have it.")
        }
        if(!has(invTeapot)) {
            print("You remove the teapot from the stove.")
            erase(objTeapot)
            get(invTeapot)
            addn(vCurrentScore, 1)    
        }
    }
    if(!posn(objEgo, 104, 93, 123, 112)) {
        print("Get closer.")
    }
}

if(said("take", "hat")) {
    if(posn(objEgo, 117, 131, 129, 150)) {
        print("You pocket your hat.")
        set(fHasHat)
        erase(objHat)
        get(invHat)
        addn(vCurrentScore, 1)
    }
    if(!posn(objEgo, 117, 131, 129, 150)) {
        print("You cannot reach it.")
    }
}

if(said("take", "teacup")) {
    if(posn(objEgo, 49, 107, 56, 135)) {
        erase(objTeaCup)
        if(equaln(vTeaPoured, valTrue)) {
            print("You pick up the teacup full of hot clover tea and carry it with you.")
            get(invTea)
        }
        if(!equaln(vTeaPoured, valTrue)) {
            print("You pick up the teacup and carry it with you.")
            get(invTeaCup)            
        }
    }
    if(!posn(objEgo, 49, 107, 56, 135)) {
        print("It is not within your reach.")
    }
}

if(said("drop", "teacup")) {
    if(posn(objEgo, 49, 107, 56, 135)) {
        print("You place the teacup on the table.")
        draw(objTeaCup)
        drop(invTeaCup)
    }
    if(!posn(objEgo, 49, 107, 56, 135)) {
        print("Get closer.")
    }
}

if(said("pour", "tea")) {
    if(has(invTeapot)) {
        if(has(invTeaCup)) {
            print("The tea is hot. Put the cup on the table.")
        }
        if(!has(invTeaCup)) {
            if(equaln(vTeaPoured, valTrue)) {
                print("the teapot is empty.")
            }
            if(!equaln(vTeaPoured, valTrue)) {
                if(posn(objEgo, 49, 107, 56, 135)) {
                    print("You pour a cup of hot tea.")
                    assignn(vTeaPoured, valTrue)
                    addn(vCurrentScore, 1)
                }
                if(!posn(objEgo, 49, 107, 56, 135)) {
                    print("You should get closer.")
                }
            }
        }
    }
    if(!has(invTeapot)) {
        print("You can't do that from here.")
    }
}

if(said("take", "tea")) {
    if(posn(objEgo, 49, 107, 56, 135)) {
        if(equaln(vTeaPoured, valTrue)) {
            get(invTea)        
            erase(objTeaCup)
            print("Ok")
        }
        if(!equaln(vTeaPoured, valTrue)) {
            print("The cup is empty.")
        }
    }
    if(!posn(objEgo, 49, 107, 56, 135)) {
        if(equaln(vTeaPoured, valTrue)) {
            print("You are not close enough.")
        }
        if(!equaln(vTeaPoured, valTrue)) {
            print("There's tea in the teapot.")
        }
    }  
}

if(said("drink", "tea")) {
    if(!has(invTea)) {
        print("You don't have it.")
    }
    if(has(invTea)) {
        print("You drink the tea. In a few minutes you begin to feel much more relaxed.")
        assignn(vTeaDrank, valTrue)
        drop(invTea)
        get(invTeaCup)
        addn(vCurrentScore, 1)    
    }
}    

if(equaln(vSleeping, valTrue)) {
    if(equaln(vClockTicks, 1)) {
        print("Time passes...")
    }

    if(equaln(vClockTicks, 2)) {
        print("More time passes...")
    }

    if(equaln(vClockTicks, 3)) {
        print("What on earth was in that tea?!?")
    }

    if(equaln(vClockTicks, 4)) {
        assignn(vEgoX, 0)
        assignn(vEgoY, 0)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picHanger)
    }
}

if(equaln(vClockTicks, 20)) {
    print("It sure is getting late")
}
if(equaln(vClockTicks, 40)) {
    print("If you don't get to sleep you'll be a real mess in the morning.")
}
if(equaln(vClockTicks, 60)) {
    // DEAD: OVERSLEPT
    assignn(vDeathReason, valXsOversleep)
    set(fDead)
}

if(equaln(vTimer, 100)) {
    assignn(vTimer, 0)
    addn(vClockTicks, 1) 
}

addn(vTimer, 1) 

return()
#message 1 "Grahams House"