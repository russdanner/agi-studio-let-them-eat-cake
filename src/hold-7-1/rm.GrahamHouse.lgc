[*********************************************************************
[ 
[ rm.GrahamHouse
[
[*********************************************************************

[***************************************
[ LOCAL DEFINES
[***************************************
[ none

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if (newRoom) {
    [ load, draw and discard the picture resource for the current room
    load.pic(currentRoom);
    draw.pic(currentRoom);
    discard.pic(currentRoom);
    
    [ do basic room initialization
    call(lgc.RoomInit);
        
    load.view(vw.GrahamNoHat);
    load.view(vw.Summons);
    load.view(vw.Hat);
    load.view(vw.Teapot);
    load.view(vw.Teacup);
    load.view(vw.GrahamHouseWindow);
    load.view(vw.GrahamHouseDoor);
    load.view(vw.GrahamHatNoFeather);
    
    animate.obj(ego);
    animate.obj(objHat);
    animate.obj(objTeapot);
    animate.obj(objTeaCup);    
    animate.obj(objSummons);    
    animate.obj(objWindow);    
    animate.obj(objCabinDoor);    
    
    set.view(ego, vw.GrahamNoHat);
    set.view(objHat, vw.Hat);
    set.view(objTeapot, vw.Teapot);
    set.view(objTeaCup, vw.Teacup);
    set.view(objSummons, vw.Summons);
    set.view(objWindow, vw.GrahamHouseWindow);
    set.view(objCabinDoor, vw.GrahamHouseDoor);
    
    start.cycling(ego);
    stop.cycling(objHat);
    stop.cycling(objTeaCup);
    stop.cycling(objSummons);
    stop.cycling(objWindow);
    stop.cycling(objCabinDoor);
    
    set.cel(objCabinDoor, 1);
    position(objCabinDoor, 26, 131);      
    position(objWindow, 90, 58);

    if(isset(fIsMorning)) {
        [switching from roger back to graham
        drop("Keycard");
        drop("Glass");
        drop("Cake");
        drop("Ink");
        drop("Quill");
        drop("Invitation");
    
        drop("Tea");
        get("Teacup");
        get("Teapot");
    
        if(isset(fHasHat)) {
            get("Hat");
        }        
        if(isset(fHasSummons)) {
            get("Summons");
        }        
    }
    
    if(!has("Hat")) {
        set.priority(objHat, 15);
        position(objHat, 130, 125);
        draw(objHat);
    }
    
    if(!has("Summons")) {
        set.priority(objSummons, 15);
        position(objSummons, 80, 105);
        draw(objSummons);
    }
    
    if(!has("Teacup")) {    
        set.priority(objTeaCup, 15);
        position(objTeaCup, 55, 105);
        draw(objTeaCup);
    }

    if(!has("Teapot")) {
        set.priority(objTeapot, 15);
        position(objTeapot, 110, 87);
        draw(objTeapot);
    }    

    assignn(vSleeping, valFalse);
    assignn(vClockTicks, 0);
  
    if(isset(fIsMorning)) {
        print("Thinking of the King you are jarred awake. It's morning. You must get to the castle to meet with King Edward!");
        assignn(vSleeping, valFalse);
        set.cel(objWindow, 1);
        [assignn(egoDir, STOPPED);
        [animate.obj(ego);
        [set.view(ego, vw.GrahamNoHat);
        [set.loop(ego, 0);
        [set.cel(ego, 0);
        [start.cycling(ego);
        [start.motion(ego);
        [start.update(ego);
        position(ego, 100, 80);
    }
    else {
        if ((previousRoom == rm.Title || previousRoom == 0)) {
            [ when starting or restarting need to position ego
            erase(ego);
            position(ego, 74, 150);
            assignn(vEgoX, 74);
            assignn(vEgoY, 150);
            status.line.on();
            animationInterval = NORMAL_SPEED;
        }

        if ((previousRoom == rm.GrahamLawnNight)) {
            position(ego, 35, 110);        
        }
        else {                                    
            position.v(ego, vEgoX, vEgoY);
        }
    }

    draw(objWindow);
    draw(objCabinDoor);
    draw(ego);
    
    [ display the picture on screen
    show.pic();
}

[***************************************
[ NON-INPUT CODE 
[***************************************

if(!isset(fKingMessageDisplayed)) {
  print("It is getting late and you should go to bed but you cannot sleep. Earlier today you received a summons to appear at the castle to meet with King Edward for reasons unknown.");
  set(fKingMessageDisplayed);
}        

if(equaln(vSleeping, valTrue)) {
  if(equaln(vTimer, 0)) {
    if(equaln(vClockTicks, 1)) {
      print("Time passes...");
    }
  
    if(equaln(vClockTicks, 2)) {
      print("More time passes...");
    }

    if(equaln(vClockTicks, 3)) {
      print("What on earth was in that tea?!?");
    }

    if(equaln(vClockTicks, 4)) {
      assignn(vEgoX, 0);
      assignn(vEgoY, 0);
      assignn(vRoomInited, valFalse);
      new.room(rm.InnerHanger);
    } 
  }
}

if(!isset(fIsMorning) && equaln(vTimer, 0)) {
  if(equaln(vClockTicks, 20)) {
    print("It sure is getting late");
  }
  if(equaln(vClockTicks, 40)) {
    print("If you don't get to sleep you'll be a real mess in the morning.");
  }
  if(equaln(vClockTicks, 60)) {
      [DEAD: OVERSLEPT
      assignn(vDeathReason, valXsOversleep);
      set(fDead);
  }
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************

[ if player has provided input and match not yet found,
[ do said tests
if (haveInput && !haveMatch) {  
    if(said("look")) {
        if(!has("Hat")) {
            print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter with a hat on it in the corner.");
        }
        if(has("Hat")) {
            print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter in the corner.");
        }
    }
    
    if(said("sleep")) {
        if(posn(ego, 75, 30, 115, 85)) {
            if(!equaln(vTeaDrank, valTrue)) {
                print("You are a bit on edge about the summons you received from the king. You can't sleep.");
            }
            if(equaln(vTeaDrank, valTrue)) {
                assignn(vSleeping, valTrue);
                assignn(vTimer, 0);
                assignn(vClockTicks, 0);
                addn(vCurrentScore, 1);    
                print("Having taken the tea you climb into bed and doze off.");

                 erase(ego);
                  load.view(vw.GrahamSleep);
                  set.view(objGrahamSleep, vw.GrahamSleep);
                  animate.obj(objGrahamSleep);  
                  position(objGrahamSleep, 110, 82);
                  set.priority(objGrahamSleep, 15);
                  draw(objGrahamSleep);

            }
        }
        if(!posn(ego, 75, 30, 115, 85)) {
            print("You're not close enough.");
        }
    }
    
    if(said("go", "sleep")) {
        if(posn(ego, 75, 30, 115, 85)) {
            if(!equaln(vTeaDrank, valTrue)) {
                print("You are a bit on edge about the summons you received from the king. You can't sleep.");
            }
            if(equaln(vTeaDrank, valTrue)) {
                assignn(vSleeping, valTrue);
                assignn(vTimer, 0);
                assignn(vClockTicks, 0);
                addn(vCurrentScore, 1)  ;  
                print("Having taken the tea you climb into bed and doze off.");
            }
        }
        if(!posn(ego, 75, 30, 115, 85)) {
            print("You're not close enough.");
        }
    }
    
    if(said("look", "room")) {
        if(!has("Hat")) {
            print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter with a hat on it in the corner.");
        }
        if(has("Hat")) {
            print("This is Graham's home. It's one of many homes in the kingdom of Daventry. Graham keeps the place neat and tidy. In the center of the room is a large table. There's small counter in the corner.");
        }
    }
    if(said("look", "stove")) {
        if(has("Teapot")) {
            print("The stove is made of cast iron and keeps the room toasty.");
        }
        if(!has("Teapot")) {
            print("The stove is made of cast iron and keeps the room toasty. There's a teapot on top of the stove.");
        }
    }
    
    if(said("look", "table")) {
        if(has("Summons")) {
            if(!has("Teacup")) {
                print("There is a sturdy table here. A teacup sits on table");
            }
            if(has("Teacup")) {
                print("There is a sturdy table here.");
            }
        }
        if(!has("Summons")) {
            if(!has("Teacup")) {
                print("There is a sturdy table here. The summons you received from the King today lays on one side of the table and a teacup sits on the opposite side.");
            }
            if(has("Teacup")) {
                print("There is a sturdy table here. The summons you received from the King today lays on the table.");
            }
        }
    }
    
    if(said("open", "door")) {
        if(posn(ego, 16, 108, 35, 124)) {
            if(!has("Hat")) {
                print("You would never leave the house without your hat.");
            }
            if(has("Hat")) {
                if(isset(fIsMorning)) {
                    addn(vCurrentScore, 1);    
                    get.posn(ego, vEgoX, vEgoY);
                    assignn(vEgoX, 119);
                    assignn(vEgoY, 126);
                    assignn(vRoomInited, valFalse);
                    new.room(rm.GrahamLawnDay);     
                }
                else {
                    get.posn(ego, vEgoX, vEgoY);
                    assignn(vEgoX, 119);
                    assignn(vEgoY, 126);
                    assignn(vRoomInited, valFalse);
                    new.room(rm.GrahamLawnNight);     
                }
            }
        }
        else {
            print("You can't do that from here.");
        }
    }
    
    if(said("wear", "hat")) {
        if(has("Hat")) {
            if(isset(fWearingHat)) {
                print("You are already wearing it.");
            }
            else {
                set(fWearingHat);
                set.view(ego, vw.GrahamHatNoFeather);
                print("You put on your trusty hat!");
    
                if(!isset(fPointsForHatWear)) {
                    set(fPointsForHatWear);
                    addn(vCurrentScore, 1);    
                }
            }
        }
        else {
            print("You don't have it.");
        }
    }   
    
    if(said("remove", "hat")) {
        if(has("Hat")) {
            if(!isset(fWearingHat)) {
                print("You are not wearing your hat.");
            }
            else {
                reset(fWearingHat);
                set.view(ego, vw.GrahamHatNoFeather);
            }
        }
        else {
            print("You don't have it.");
        }
    }  
    
    if(said("take", "summons")) {
        if(posn(ego, 80, 100, 100, 130)) {
            if(has("Summons")) {
                print("You already have it.");
            }
            else {
                print("You take the summons.");
                set(fHasSummons);
                get("Summons");
                erase(objSummons);
                addn(vCurrentScore, 1);        
            }
        }
        else {
            print("Get closer.");
        }
    }
    
    if(said("take", "teapot")) {
        if(posn(ego, 104, 93, 123, 112)) {
            if(has("Teapot")) {
                print("You alread have it.");
            }
            else {
                print("You remove the teapot from the stove.");
                erase(objTeapot);
                get("Teapot");
                addn(vCurrentScore, 1);
            }
        }
        else {
            print("Get closer.");
        }
    }
    
    if(said("take", "hat")) {
        if(posn(ego, 117, 131, 129, 150)) {
            print("You pocket your hat.");
            set(fHasHat);
            erase(objHat);
            get("Hat");
            addn(vCurrentScore, 1);
        }
        else {
            print("You cannot reach it.");
        }
    }
    
    if(said("take", "teacup")) {
        if(posn(ego, 40, 107, 56, 135)) {
            erase(objTeaCup);
            if(equaln(vTeaPoured, valTrue)) {
                print("You pick up the tetakeacup full of hot clover tea and carry it with you.");
                get("Tea");
            }
            else {
                print("You pick up the teacup and carry it with you.");
                get("Teacup");            
            }
        }
        else {
            print("It is not within your reach.");
        }
    }
    
    if(said("drop", "teacup")) {
        if(posn(ego, 40, 107, 56, 135)) {
            print("You place the teacup on the table.");
            draw(objTeaCup);
            drop("Teacup");
        }
        else {
            print("Get closer.");
        }
    }
    
    if(said("pour", "tea")) {
        if(has("Teapot")) {
            if(has("Teacup")) {
                print("The tea is hot. Put the cup on the table.");
            }
            else {
                if(equaln(vTeaPoured, valTrue)) {
                    print("the teapot is empty.");
                }
                else {
                    if(posn(ego, 40, 107, 56, 135)) {
                        print("You pour a cup of hot tea.");
                        assignn(vTeaPoured, valTrue);
                        addn(vCurrentScore, 1);
                    }
                    else {
                        print("You should get closer.");
                    }
                }
            }
        }
        else {
            print("You can't do that from here.");
        }
    }
    
    if(said("take", "tea")) {
        if(posn(ego, 40, 107, 56, 135)) {
            if(equaln(vTeaPoured, valTrue)) {
                get("Tea");
                erase(objTeaCup);
                print("Ok");
            }
            else {
                print("The cup is empty.");
            }
        }
        else {
            if(equaln(vTeaPoured, valTrue)) {
                print("You are not close enough.");
            }
            else {
                print("There's tea in the teapot.");
            }
        }  
    }
    
    if(said("drink", "tea")) {
        if(!has("Tea")) {
            print("You don't have it.");
        }
        else {
            print("You drink the tea. In a few minutes you begin to feel much more relaxed.");
            assignn(vTeaDrank, valTrue);
            drop("Tea");
            get("Teacup");
            addn(vCurrentScore, 1); 
        }
    }    
}

:done
return();

[ DECLARED MESSAGES
[***************************************
