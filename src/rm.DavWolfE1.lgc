[*********************************************************************
[
[ rm.DavWolfE1
[
[ 
[
[*********************************************************************

[***************************************
[ LOCAL DEFINES
[***************************************

[ add local defines here

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if(newRoom) {
    [ load, draw and discard the picture for the current room
    load.pic(currentRoom);
    draw.pic(currentRoom);
    discard.pic(currentRoom);

    unanimate.all();
    assignn(vRoomInited, valTrue);

    load.view(vw.Roger);
    load.view(vw.Mist);

    animate.obj(ego);
    animate.obj(objMist);
    animate.obj(objMist2);
    animate.obj(objMist3);
    animate.obj(objMist4);

    set.view(ego, vw.Roger);
    set.view(objMist, vw.Mist);
    set.view(objMist2, vw.Mist);
    set.view(objMist3, vw.Mist);
    set.view(objMist4, vw.Mist);

    stop.cycling(objMist);
    stop.cycling(objMist2);
    stop.cycling(objMist3);
    stop.cycling(objMist4);

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)) {
            assignn(vEgoX, 100);
            assignn(vEgoY, 100);
        }
    }

    ignore.objs(objMist);
    ignore.objs(objMist2);
    ignore.objs(objMist3);
    ignore.objs(objMist4);
    
    position(objMist, 1, 43);
    position(objMist2, 43, 43);
    position(objMist3, 84, 43);
    position(objMist4, 120, 43);    
    

    [ add wider range to reduce instances of encounter
    reset(fGotcha);

    random(1, 100, vDiceRoll);

    if(lessn(vDiceRoll, 50)) {
        load.view(vw.Wolf);
        set.view(objMonster, vw.Wolf);
        animate.obj(objMonster);
        position(objMonster, 80, 80);
        draw(objMonster);        
        follow.ego(objMonster, 8, fGotcha);
 
        load.sound(snd.MonsterAlert);
        sound(snd.MonsterAlert, fSoundDone);    
    }
    
    position.v(ego, vEgoX, vEgoY);

    draw(ego);
    draw(objMist);
    draw(objMist2);
    draw(objMist3);
    draw(objMist4);

    set.horizon(50);

    show.pic();
}

[***************************************
[ NON-INPUT CODE 
[***************************************

if(edgeEgoHit == TOP_EDGE) {
    print("You're a bit foggy here. You decide to turn back.");
    get.posn(ego, vEgoX, vEgoY);
    assignn(vEgoY, 55);
    assignn(vEgoDirection, DOWN);
    erase(ego);
    position.v(ego, vEgoX, vEgoY);
    draw(ego);
}

if(edgeEgoHit == LEFT_EDGE) {
    get.posn(ego, vEgoX, vEgoY);
    assignn(vEgoX, 150);
    assignn(vRoomInited, valFalse);
    new.room(rm.DavWellTopD1);     
}

if(edgeEgoHit == RIGHT_EDGE) {
    get.posn(ego, vEgoX, vEgoY);
    assignn(vEgoX, 5);
    assignn(vEgoY, 100);
    assignn(vRoomInited, valFalse);
    new.room(rm.DavTreeF1);     
}

if(edgeEgoHit == BOTTOM_EDGE) {
    get.posn(ego, vEgoX, vEgoY);
    assignn(vEgoY, 50);
    assignn(vRoomInited, valFalse);
    new.room(rm.DavBigLakeRightE2);     
}

if(isset(fGotcha)) {
    reset(fGotcha);
    
    if(!isset(fDead)) { 
        erase(ego);
        load.view(vw.FightCloud);
        set.view(objMonster, vw.FightCloud);
        normal.motion(objMonster);
        assignn(vDeathReason, valXsWolfMunch);
        stop.cycling(objMonster);
        set.cel(objMonster, 0);
        stop.motion(objMonster);
        end.of.loop(objMonster, fDead);            
    }
}


[***************************************
[ PROCESS PLAYER INPUT
[***************************************
if (haveInput && !haveMatch && unknownWordNum == 0) {
    if((said("look", "room") || said("look"))) {
        print("The area is covered by bushes and thick brush. There is a thick fog to the north.");
    }

    if(said("look", "fog")) {
        print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.");
    }
}

[***************************************
[ CHECK FOR AN EXIT CONDITION
[***************************************
:exit

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "You see nothing of interest." 
