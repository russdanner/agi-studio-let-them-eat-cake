[*********************************************************************
[
[ rm.DavLakeE2
[
[ 
[
[*********************************************************************

[***************************************
[ LOCAL DEFINES
[***************************************

[ add local defines here

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if(newRoom) {
    [ load, draw and discard the picture for the current room
    load.pic(currentRoom);
    draw.pic(currentRoom);
    discard.pic(currentRoom);
    
    unanimate.all();
    
    assignn(vRoomInited, 1);

    load.view(vw.Roger);
    load.view(vw.RogerSplash);
    load.view(vw.RogerSwim);
    load.view(vw.Mist);

    animate.obj(ego);
    animate.obj(objMist);
    animate.obj(objMist2);
    animate.obj(objMist3);
    animate.obj(objMist4);

    set.view(ego, vw.Roger);
    set.view(objMist, vw.Mist);
    set.view(objMist2, vw.Mist);
    set.view(objMist3, vw.Mist);
    set.view(objMist4, vw.Mist);

    stop.cycling(objMist);
    stop.cycling(objMist2);
    stop.cycling(objMist3);
    stop.cycling(objMist4);

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)) {
            assignn(vEgoX, 100);
            assignn(vEgoY, 100);
        }
    }
    
    position(objMist, 1, 166);
    position(objMist2, 43, 166);
    position(objMist3, 85, 166);
    position(objMist4, 127, 166);

    position.v(ego, vEgoX, vEgoY);

    draw(ego);
    draw(objMist);
    draw(objMist2);
    draw(objMist3);
    draw(objMist4);
    
    set.horizon(40);
    
    show.pic();
}

[***************************************
[ NON-INPUT CODE 
[***************************************

if(edgeEgoHit == TOP_EDGE) {
    assignn(vEgoY, 150);
    assignn(vRoomInited, valFalse);
    [MR1 new.room(rm.DavWolfE1);   
}

if(edgeEgoHit == LEFT_EDGE) {
    get.posn(ego, vEgoX, vEgoY);
    assignn(vEgoX, 150);
    assignn(vRoomInited, valFalse);
    new.room(rm.DavLakeE2);     
}

if(edgeEgoHit == RIGHT_EDGE) {
    get.posn(ego, vEgoX, vEgoY);
    assignn(vEgoX, 5);
    assignn(vEgoY, 100);
    assignn(vRoomInited, valFalse);
    new.room(rm.DavDaggerF2);     
}

if(edgeEgoHit == BOTTOM_EDGE) {
    print("You're a bit foggy here. You decide to turn back.");
    get.posn(ego, vEgoX, vEgoY);
    assignn(vEgoY, 155);
    assignn(egoDir, UP);
    position.v(ego, vEgoX, vEgoY);
}

if(isset(fSysEgoOnWater)) {
    if(!isset(fSwimming)) {
        if(!isset(fDrowning)) {
            set.view(ego, vw.RogerSplash);
            set(fDrowning);
        }
    }    
}
else {
    set.view(ego, vw.Roger);
    reset(fSwimming);
    reset(fDrowning);
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************
if (haveInput && !haveMatch && unknownWordNum == 0) {
    if(said("swim")) {
        if(isset(fSysEgoOnWater)) {
            if(isset(fDrowning)) {
                set.view(ego, vw.RogerSwim);
                reset(fDrowning);
                set(fSwimming);
                player.control();
                print("ok");
           } 
        }
    }
    
    if((said("look", "room") || said("look"))) {
        print("There's a large beautiful lake that goes off to the west here and a thick fog to the south.");
    }
    if(said("look", "fog")) {
        print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.");
    }

    if(said("look", "water")) {
        print("The lake is calm and beautiful.");
    }
}

[***************************************
[ CHECK FOR AN EXIT CONDITION
[***************************************
:exit

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "You see nothing of interest." 
